; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "pyTivo"
#define MyAppVersion "1.6.2"
#define MyAppPublisher "pyTivo"
#define MyAppURL "http://pytivo.org/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{74DAD9B2-BCE1-41C0-A3E7-DBEEA08C07DB}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputDir=..\install builds\win32\
OutputBaseFilename=pyTivo_{#MyAppVersion}
SetupIconFile=..\desktop\icon.ico
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin

AppMutex=pyTivoTray_{{BF213038-4019-49C0-A0AD-9D4419852647}
;CloseApplications=yes
UninstallDisplayName=pyTivo
UninstallDisplayIcon={app}\pyTivo.exe
WizardImageFile=userdocs:pyTivoDesktop\installer\resources\left.bmp
WizardSmallImageFile=userdocs:pyTivoDesktop\installer\resources\logo.bmp

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "..\build\pyTivoTray.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\build\pyTivo.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\build\pyTivoService.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\build\dshow\tivomak.exe"; DestDir: "{app}\dshow"; Flags: ignoreversion; Tasks: InstallDSFilter
Source: "..\build\dshow\TiVoDirectShowFilter.dll"; DestDir: "{app}\dshow"; Flags: ignoreversion regserver sharedfile overwritereadonly uninsremovereadonly replacesameversion; Tasks: InstallDSFilter
Source: "..\build\dshow\TiVoTranscoder.Common.manifest"; DestDir: "{app}\dshow"; Flags: ignoreversion; Tasks: InstallDSFilter
Source: "..\build\dshow\MainConcept\MainConcept.manifest"; DestDir: "{app}\dshow\MainConcept"; Flags: ignoreversion; Tasks: InstallDSFilter
Source: "..\build\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\build\desktop\*"; DestDir: "{app}\desktop"; Flags: ignoreversion createallsubdirs recursesubdirs

[Icons]
Name: "{group}\pyTivo"; Filename: "{app}\pyTivoTray.exe"
Name: "{group}\pyTivo Desktop"; Filename: "{app}\desktop\pyTivoDesktop.exe"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\dshow\tivomak.exe"; Parameters: "-set {code:GetTivoMAK}"; Flags: runasoriginaluser; Tasks: InstallDSFilter
Filename: "{app}\pyTivoTray.exe"; Parameters: "--show-desktop --mak {code:GetTivoMAK} --path ""{code:GetTivoPath}""{code:GetTivoPublishPath}"; Flags: nowait runasoriginaluser postinstall; Description: "Start pyTivo Now"

[Registry]
Root: "HKLM32"; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "pyTivo"; ValueData: "{app}\pyTivoTray.exe"; Flags: uninsdeletevalue; Tasks: StartWithWindows

[Tasks]
Name: "StartWithWindows"; Description: "Start automatically when Windows starts"
Name: "InstallDSFilter"; Description: "Install TiVo DirectShow filter (only use this option if you do not have TiVo Desktop installed)"

[Dirs]
Name: "{app}\desktop\desktop"
Name: "{app}\desktop\locales\locales"
Name: "{app}\desktop\resources\resources"
Name: "{app}\bin\jre\"
Name: "{app}\bin\jre\bin\"
Name: "{app}\bin\jre\bin\client\"
Name: "{app}\bin\jre\bin\dtplugin\"
Name: "{app}\bin\jre\bin\plugin2\"
Name: "{app}\bin\jre\lib\"
Name: "{app}\bin\jre\lib\applet\"
Name: "{app}\bin\jre\lib\cmm\"
Name: "{app}\bin\jre\lib\deploy\"
Name: "{app}\bin\jre\lib\ext\"
Name: "{app}\bin\jre\lib\fonts\"
Name: "{app}\bin\jre\lib\i386\"
Name: "{app}\bin\jre\lib\images\"
Name: "{app}\bin\jre\lib\images\cursors\"
Name: "{app}\bin\jre\lib\jfr\"
Name: "{app}\bin\jre\lib\management\"
Name: "{app}\bin\jre\lib\security\"
Name: "{app}\bin\jre\"
Name: "{app}\bin\jre\bin\"
Name: "{app}\bin\jre\bin\client\"
Name: "{app}\bin\jre\bin\dtplugin\"
Name: "{app}\bin\jre\bin\plugin2\"
Name: "{app}\bin\jre\lib\"
Name: "{app}\bin\jre\lib\applet\"
Name: "{app}\bin\jre\lib\cmm\"
Name: "{app}\bin\jre\lib\deploy\"
Name: "{app}\bin\jre\lib\ext\"
Name: "{app}\bin\jre\lib\fonts\"
Name: "{app}\bin\jre\lib\i386\"
Name: "{app}\bin\jre\lib\images\"
Name: "{app}\bin\jre\lib\images\cursors\"
Name: "{app}\bin\jre\lib\jfr\"
Name: "{app}\bin\jre\lib\management\"
Name: "{app}\bin\jre\lib\security\"
Name: "{app}\bin\jre\bin\"
Name: "{app}\bin\jre\bin\client\"
Name: "{app}\bin\jre\bin\dtplugin\"
Name: "{app}\bin\jre\bin\plugin2\"
Name: "{app}\bin\jre\lib\"
Name: "{app}\bin\jre\lib\applet\"
Name: "{app}\bin\jre\lib\cmm\"
Name: "{app}\bin\jre\lib\deploy\"
Name: "{app}\bin\jre\lib\ext\"
Name: "{app}\bin\jre\lib\fonts\"
Name: "{app}\bin\jre\lib\i386\"
Name: "{app}\bin\jre\lib\images\"
Name: "{app}\bin\jre\lib\images\cursors\"
Name: "{app}\bin\jre\lib\jfr\"
Name: "{app}\bin\jre\lib\management\"
Name: "{app}\bin\jre\lib\security\"

[Code]
var
  TivoOptsPage: TWizardPage;
  TivoMAKEdit: TEdit;
  TivoPathEdit: TEdit;
  TivoPathBrowseButton: TButton;
  TivoPathPublish: TCheckBox;

function GetTivoMAK(Parameter: string): string;
begin
  Result := TivoMAKEdit.Text;
end;

function GetTivoPath(Parameter: string): string;
begin
  Result := TivoPathEdit.Text;
end;

function GetTivoPublishPath(Parameter: string): string;
begin
  if TivoPathPublish.Checked then 
    begin
      Result := ' --publish';
    end
  else
    begin 
      Result := '';
    end
end;

procedure BrowseClick(Sender : TObject);
var
 Dir : String;
begin
  Dir := TivoPathEdit.Text;
  if BrowseForFolder('Select Folder',Dir,false) then
    TivoPathEdit.Text := Dir;
end;

procedure TivoMAKEditKeyPress(Sender: TObject; var Key: Char);
var
  KeyCode: Integer;
begin
  // allow only numbers
  KeyCode := Ord(Key);
  if not ((KeyCode = 8) or ((KeyCode >= 48) and (KeyCode <= 57))) then
    Key := #0;
end;

function AuthPage(Page: TWizardPage) : Boolean;
var
  MAK : String;
  Dir : String;
begin
  Result := True;
  MAK := TivoMAKEdit.Text;
  Dir := TivoPathEdit.Text;

  if MAK <> '' then
    begin
      if Length(MAK) < 10 then
        begin
          MsgBox('Invalid Media Access Key', mbError, MB_OK);
          Result := False;
        end
      else
        begin
          if Dir <> '' then
            begin
              if not DirExists(Dir) then
                begin
                  MsgBox('TiVo recordings folder must be a valid path', mbError, MB_OK);
                  Result := False;
                end
            end
          else
            begin
              MsgBox('Please select a TiVo recordings folder', mbError, MB_OK);
              Result := False; 
            end
        end
    end
  else
    begin
      MsgBox('Please enter your TiVo Media Access Key', mbError, MB_OK);
      Result := False;
    end    
end; 

procedure InitializeWizard;
var
  TivoMAKLabel: TLabel;
  TivoPathLabel: TLabel;

begin
  TivoOptsPage := CreateCustomPage(wpSelectDir, 'Set options for ToGo functions', '');
  TivoOptsPage.OnNextButtonClick := @AuthPage;

  TivoMAKLabel := TLabel.Create(WizardForm);
  TivoMAKLabel.Parent := TivoOptsPage.Surface;
  TivoMAKLabel.Left := 0;
  TivoMAKLabel.Top := 0;
  TivoMAKLabel.Caption := 'TiVo Media Access Key';

  TivoMAKEdit := TEdit.Create(WizardForm);
  TivoMAKEdit.Parent := TivoOptsPage.Surface;
  TivoMAKEdit.Left := 0;
  TivoMAKEdit.Top :=  TivoMAKLabel.Top + TivoMAKLabel.Height + 6;
  TivoMAKEdit.Width := 300;
  TivoMAKEdit.MaxLength := 10;  
  TivoMAKEdit.OnKeyPress := @TivoMAKEditKeyPress;  
  TivoMAKEdit.Text := GetIniString('Server', 'tivo_mak', '', ExpandConstant('{userappdata}')+'\{#MyAppName}\{#MyAppName}.conf');

  TivoPathLabel := TLabel.Create(WizardForm);
  TivoPathLabel.Parent := TivoOptsPage.Surface;
  TivoPathLabel.Left := 0;
  TivoPathLabel.Top := TivoMAKEdit.Top + TivoMAKEdit.Height + 12;
  TivoPathLabel.Caption := 'TiVo recordings folder'

  TivoPathEdit := TEdit.Create(WizardForm);
  TivoPathEdit.Parent := TivoOptsPage.Surface;
  TivoPathEdit.Left := 0;
  TivoPathEdit.Top :=  TivoPathLabel.Top + TivoPathLabel.Height + 6;
  TivoPathEdit.Width := 300;  
  TivoPathEdit.Text := GetIniString('Server', 'togo_path', '', ExpandConstant('{userappdata}')+'\{#MyAppName}\{#MyAppName}.conf');

  TivoPathBrowseButton := TButton.Create(WizardForm);
  TivoPathBrowseButton.Parent := TivoOptsPage.Surface;
  TivoPathBrowseButton.Left := TivoPathEdit.Left + TivoPathEdit.Width + 6;
  TivoPathBrowseButton.Top :=  TivoPathEdit.Top;
  TivoPathBrowseButton.Height := TivoPathEdit.Height;
  TivoPathBrowseButton.Width := 70;
  TivoPathBrowseButton.Caption := 'Browse';
  TivoPathBrowseButton.OnClick := @BrowseClick;   
  
  TivoPathPublish := TCheckBox.Create(WizardForm);
  TivoPathPublish.Parent := TivoOptsPage.Surface;
  TivoPathPublish.Left := 0;
  TivoPathPublish.Top :=  TivoPathEdit.Top + TivoPathEdit.Height + 6;
  TivoPathPublish.Width := 300; 
  TivoPathPublish.Caption := 'Publish recordings folder as video share';
  TivoPathPublish.Checked := true;
      
end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
  ResultCode: Integer;
  Cmd: String;
begin
  Result := True;
  if (CurPageID = wpFinished) and (WizardForm.RunList.Items.Count > 0) and (WizardForm.RunList.Checked[0] = False) then
    begin
      Cmd := '--silent --mak ' + GetTivoMAK('') + ' --path "' + GetTivoPath('') + '"' + GetTivoPublishPath('');
      ExecAsOriginalUser(ExpandConstant('{app}\pyTivoTray.exe'), Cmd, '',  SW_SHOWNORMAL, ewNoWait, ResultCode);
    end
end;
